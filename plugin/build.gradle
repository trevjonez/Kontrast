/*
 *    Copyright 2017 Trevor Jones
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

apply plugin: 'nebula.kotlin'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven'

group = rootProject.group
version = rootProject.version
sourceCompatibility = 1.8

gradlePlugin {
    plugins {
        kontrast {
            id = "kontrast"
            implementationClass = "com.trevjonez.kontrast.KontrastPlugin"
        }
    }
}

dependencies {
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-html-jvm', version: kotlinx_html_version
    compile group: 'com.android.tools.build', name: 'gradle', version: android_plugin_version
    compile group: 'io.reactivex.rxjava2', name: 'rxjava', version: '2.1.1'
    compile group: 'azadev.kotlin', name: 'aza-kotlin-css', version: '1.0'
    compile group: 'com.squareup.moshi', name:'moshi', version: '1.5.0'
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.5'
}

configurations {
    reportResources
}

dependencies {
    reportResources project(':reportJs')
}

def mdcVersion = '0.16.0'

task downloadMdcCss(type: DownloadFile) {
    url="https://unpkg.com/material-components-web@$mdcVersion/dist/material-components-web.min.css"
    target = file("$buildDir/material-components-web.min.css")
    group = "Download Resources"
    description = "Downloads MDC css file"
}

task downloadMdcJs(type: DownloadFile) {
    url="https://unpkg.com/material-components-web@$mdcVersion/dist/material-components-web.min.js"
    target = file("$buildDir/material-components-web.min.js")
    group = "Download Resources"
    description = "Downloads MDC js file"
}

processResources {
    dependsOn(':reportJs:jar')
    configurations.reportResources.each { File file ->
        from zipTree(file.absolutePath)
    }
    from downloadMdcCss
    from downloadMdcJs
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    systemProperty("KontrastVersion", version)
}

tasks.findByPath('test').dependsOn(':plugin:install')
tasks.findByPath('test').dependsOn(':unitTestClient:install')
tasks.findByPath('test').dependsOn(':androidTestClient:install')
tasks.findByPath('test').dependsOn(':appClient:install')

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.artifactId = 'plugin'
        pom.project {
            inceptionYear '2017'
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
        }
    }
}

class DownloadFile extends DefaultTask {
    @Input
    String url

    @OutputFile
    File target

    @TaskAction
    void download() { ant.get(src: url, dest: target) }
}